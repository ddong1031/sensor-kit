//apply plugin: 'com.android.library'
//apply plugin: 'maven-publish'
plugins {
    id 'com.android.library'
    id 'maven-publish'
}
// 在 android {} 块外面添加
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

android {
    compileSdkVersion 30


    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "default"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lint {
        abortOnError false
    }
    namespace 'com.sensoro.libbleserver'

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.?ar'])
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "com.google.protobuf:protobuf-java:2.5.0"
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'no.nordicsemi.android:dfu:1.11.0'
    //
}

publishing {
    publications {
        release(MavenPublication) {
            from components.findByName("release")
            groupId = 'com.ddong1031.sensor-kit' // 替换为你的 Group ID
            artifactId = 'sensor-kit' // 替换为你的库名称
            version = '1.7.6' // 设置库版本

            // 包含源码 JAR
            artifact sourcesJar
        }
    }

    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/ddong1031/sensor-kit")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
            }
        }
    }
}
